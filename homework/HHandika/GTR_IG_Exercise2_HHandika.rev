data <- readDiscreteCharacterData("data/primates_and_galeopterus_cytb.nex")

#Calculate number of taxa and branch lenght. Branch length 2N*3
num_taxa <- data.ntaxa()
num_branches <- 2*num_taxa - 3
taxa <- data.taxa()

#SetupMoves
moves    = VectorMoves()
monitors = VectorMonitors()

#pi prior
pi_prior <- v(1,1,1,1) 
pi ~ dnDirichlet(pi_prior)

#move
moves.append( mvBetaSimplex(pi, weight=2) )
moves.append( mvDirichletSimplex(pi, weight=1) )

#Er priors drawn from Dirichlet distribution
er_prior <- v(1,1,1,1,1,1) 
er ~ dnDirichlet(er_prior)

#Move
moves.append( mvBetaSimplex(er, weight=3) )
moves.append( mvDirichletSimplex(er, weight=1.5) )


#GTR matrix
Q := fnGTR(er,pi)

#Add Gamma
mu_a <- ln(5.0)
sigma_a <- 0.587405
alpha ~ dnLognormal( mu_a, sigma_a )

#Site rate
gamma_rates := fnDiscretizeGamma( alpha, alpha, 4 )

#Site Invariant
data.getNumInvariantSites()

pinvar ~ dnBeta(1,1)

#move 
moves.append( mvScale(alpha, weight=2.0) )
moves.append( mvSlide(pinvar) )

#Working with the topology. Set the outgroup
out_group = clade("Galeopterus_variegatus")
topology ~ dnUniformTopology(taxa, outgroup=out_group)
moves.append( mvNNI(topology, weight=num_taxa/2.0) )
moves.append( mvSPR(topology, weight=num_taxa/10.0) )

#For loop for GTR
for (i in 1:num_branches) {
   br_lens[i] ~ dnExponential(10.0)
   moves.append( mvScale(br_lens[i]) )
}


#Tree length in this case is the sum of branch length
TL := sum(br_lens)

#Creating the tree
psi := treeAssembly(topology, br_lens)

#Building a random variable for the character data
seq ~ dnPhyloCTMC(tree=psi, Q=Q, siteRates=gamma_rates, pInv=pinvar, type="DNA")

seq.clamp(data)

#Run MCMC
mymodel = model(Q)

monitors.append( mnModel(filename="outputIG/primates_cytb_GTRIG.log", printgen=10) )

monitors.append( mnFile(filename="outputIG/primates_cytb_GTRIG.trees", printgen=10, psi) )

monitors.append( mnScreen(printgen=500, TL) )

mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

mymcmc.run(generations=10000,tuningInterval=200)

treetrace = readTreeTrace("outputIG/primates_cytb_GTRIG.trees", treetype="non-clock", outgroup=out_group)

map_tree = mapTree(treetrace,"outputIG/primates_cytb_GTRIG_MAP.tree")