data <- readDiscreteCharacterData("data/primates_and_galeopterus_cytb.nex")

#Calculate number of taxa and branch lenght. Branch length 2N*3
num_taxa <- data.ntaxa()
num_branches <- 2*num_taxa - 3
taxa <- data.taxa()

#SetupMoves
moves    = VectorMoves()
monitors = VectorMonitors()

#HKY priors drawn from Dirichlet distribution
pi_prior <- v(1,1,1,1) 
pi ~ dnDirichlet(pi_prior)

#Kappa move
moves.append( mvBetaSimplex(pi, weight=2) )
moves.append( mvDirichletSimplex(pi, weight=1))

#Kappa
kappa ~ dnLognormal(0.0, 1.0)
kappa

#move for Kappa
moves.append( mvScale(kappa) )

Q := fnHKY(kappa,pi)
Q

#Working with the topology. Set the outgroup
out_group = clade("Galeopterus_variegatus")
topology ~ dnUniformTopology(taxa, outgroup=out_group)

#move setup for MCMC
moves.append( mvNNI(topology, weight=num_taxa) )
moves.append( mvSPR(topology, weight=num_taxa/10.0) )


#For loop for Kappa
for (i in 1:num_branches) {
   br_lens[i] ~ dnExponential(10.0)
   moves.append( mvScale(br_lens[i]) )
}

#Tree length in this case is the sum of branch length
TL := sum(br_lens)

#Creating the tree
psi := treeAssembly(topology, br_lens)

#Building a random variable for the character data
seq ~ dnPhyloCTMC(tree=psi, Q=Q, type="DNA")

seq.clamp(data)

#Run MCMC
mymodel = model(Q)

monitors.append( mnModel(filename="output/primates_cytb_HKY.log", printgen=10) )

monitors.append( mnFile(filename="output/primates_cytb_HKY.trees", printgen=10, psi) )

monitors.append( mnScreen(printgen=1000, TL) )

mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

mymcmc.run(generations=20000,tuningInterval=200)

treetrace = readTreeTrace("output/primates_cytb_HKY.trees", treetype="non-clock", outgroup=out_group)

map_tree = mapTree(treetrace,"output/primates_cytb_HKY_MAP.tree")