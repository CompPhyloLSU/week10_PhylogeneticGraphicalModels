data <- readDiscreteCharacterData("data/primates_and_galeopterus_cytb.nex")

data

num_taxa <- data.ntaxa()
num_branches <- 2 * num_taxa - 3
taxa <- data.taxa()

moves    = VectorMoves()
monitors = VectorMonitors()

pi_prior <- v(1,1,1,1) 
pi ~ dnDirichlet(pi_prior)

kappa ~ dnLognormal(0.0, 1.0)

out_group = clade("Galeopterus_variegatus")
topology ~ dnUniformTopology(taxa, outgroup=out_group)

Q := fnHKY(kappa,pi)

moves.append( mvNNI(topology, weight=num_taxa) )
moves.append( mvSPR(topology, weight=num_taxa/10.0) )
moves.append( mvBetaSimplex(pi, weight=2) )
moves.append( mvDirichletSimplex(pi, weight=1) )
moves.append( mvScale(kappa) )

for (i in 1:num_branches) {
   br_lens[i] ~ dnExponential(10.0)
   moves.append( mvScale(br_lens[i]) )
}

TL := sum(br_lens)

psi := treeAssembly(topology, br_lens)

seq ~ dnPhyloCTMC(tree=psi, Q=Q, type="DNA")

seq.clamp(data)

mymodel = model(Q)

mymodel

monitors.append( mnModel(filename="output/primates_cytb_HKY.log", printgen=10) )
monitors.append( mnFile(filename="output/primates_cytb_HKY.trees", printgen=10, psi) )
monitors.append( mnScreen(printgen=1000, TL) )


mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

mymcmc.run(generations=20000,tuningInterval=200)

#treetrace = readTreeTrace("output/primates_cytb_HKY.trees", treetype="non-clock", outgroup=out_group)

#map_tree = mapTree(treetrace,"output/primates_cytb_HKY_MAP.tree")

#Lemuroidea <- clade("Cheirogaleus_major", "Daubentonia_madagascariensis", "Lemur_catta", "Lepilemur_hubbardorum", "Microcebus_murinus", "Propithecus_coquereli", "Varecia_variegata_variegata")                  
#treetrace.cladeProbability( Lemuroidea )

#Lorisidae <- clade("Galago_senegalensis", "Otolemur_crassicaudatus", "Loris_tardigradus", "Nycticebus_coucang", "Perodicticus_potto")
#treetrace.cladeProbability( Lorisidae)


#Catarrhini <- clade("Chlorocebus_aethiops", "Colobus_guereza", "Macaca_mulatta", "Pan_paniscus", "Hylobates_lar")
#treetrace.cladeProbability(Catarrhini)

#Platyrrhini <- clade("Aotus_trivirgatus", "Cebus_albifrons", "Saimiri_sciureus", "Callicebus_donacophilus")
#treetrace.cladeProbability( Platyrrhini )

