data<- readDiscreteCharacterData("data/primates_and_galeopterus_cytb.nex")


num_taxa<-data.ntaxa()
num_branches <- 2 * num_taxa - 3
taxa<-data.taxa()

moves    = VectorMoves()
monitors = VectorMonitors()

out_group = clade("Galeopterus_variegatus")
topology ~ dnUniformTopology(taxa, outgroup=out_group)

pi_prior <- v(1,1,1,1) 
pi ~ dnDirichlet(pi_prior)

moves.append( mvBetaSimplex(pi, weight=2) )
moves.append( mvDirichletSimplex(pi, weight=1) )

kappa ~ dnLognormal(0.0, 1.0)
moves.append( mvScale(kappa) )

Q := fnHKY(kappa,pi)


for (i in 1:num_branches) {
   br_lens[i] ~ dnExponential(10.0)
   moves.append( mvScale(br_lens[i]) )
}

TL := sum(br_lens)

psi := treeAssembly(topology, br_lens)

seq ~ dnPhyloCTMC(tree=psi, Q=Q, type="DNA")
seq.clamp(data)


mymodel = model(Q)


monitors.append( mnScreen(TL, printgen=1000) )
monitors.append( mnFile(psi, filename="output/primates_cytb_HK.trees", printgen=10) )
monitors.append( mnModel(filename="output/primates_cytb_HK.log", printgen=10) )


mymcmc = mcmc(mymodel, moves, monitors, nruns=2, combine="mixed")
mymcmc.run(generations=10000,tuningInterval=200)



treetrace = readTreeTrace("output/primates_cytb_HK.trees", treetype="non-clock", outgroup=out_group)

map_tree = mapTree(treetrace,"output/primates_cytb_HK_MAP.tree")

Lemuroidea <- clade("Cheirogaleus_major", 
                    "Daubentonia_madagascariensis", 
                    "Lemur_catta", 
                    "Lepilemur_hubbardorum",
                    "Microcebus_murinus",
                    "Propithecus_coquereli",
                    "Varecia_variegata_variegata")
                    
treetrace.cladeProbability( Lemuroidea )

Lorisidae <- clade("Loris_tardigradus", 
                    "Galago_senegalensis", 
                    "Nycticebus_coucang", 
                    "Otolemur_crassicaudatus",
                    "Perodicticus_potto")
                    
treetrace.cladeProbability( Lorisidae )

Platyrrhini <- clade("Aotus_trivirgatus", 
                    "Callicebus_donacophilus", 
                    "Cebus_albifrons", 
                    "Saimiri_sciureus")
                    
treetrace.cladeProbability( Platyrrhini)

Catarrhini <- clade("Chlorocebus_aethiops", 
                    "Hylobates_lar", 
                    "Colobus_guereza", 
                    "Pan_paniscus",
                    "Macaca_mulatta")
                    
treetrace.cladeProbability( Catarrhini )

